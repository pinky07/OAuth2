buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

plugins {
    id 'org.springframework.boot' version '1.5.8.RELEASE'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'docker'

group = 'com.gft'
version = '0.0.1-SNAPSHOT'
description = 'GFT-OAuth2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

ext {
    gVersion = '4.3'
    springCloudVersion = 'Dalston.SR4'
}

dependencies {

    // Web
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-actuator')

    // Security
    compile('org.springframework.cloud:spring-cloud-starter-security')

    // OAuth2 + JWT
    compile('org.springframework.cloud:spring-cloud-starter-oauth2')
    compile('org.springframework.security:spring-security-jwt')

    // SAML
    compile('org.springframework.security.extensions:spring-security-saml2-core:1.0.2.RELEASE')
    compile('org.springframework.boot:spring-boot-configuration-processor')

    // Eureka
    compile('org.springframework.cloud:spring-cloud-starter-eureka')

    // Database
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile('org.flywaydb:flyway-core')
    compile("com.h2database:h2")

    // Test
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

task buildDockerCI(type: Docker, dependsOn: build) {
    tag = 'com.gft.oauth2.ci'
    tagVersion = 'latest'
    baseImage = 'openjdk:alpine'
    push = false // Would push to a Docker Hub account
    applicationName = jar.baseName
    addFile {
        from "${projectDir}/src/main/resources/workspace-int.gft.com.cer"
        into '/'
    }
    runCommand 'keytool -importcert -alias workspace-int.gft.com-1 -keystore "$JAVA_HOME/jre/lib/security/cacerts" -storepass changeit -file /workspace-int.gft.com.cer -noprompt'
    addFile {
        from jar
        rename {'app.jar'}
    }
    entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar'])
}

task buildDockerQA(type: Docker, dependsOn: build) {
    tag = 'com.gft.oauth2.qa'
    tagVersion = 'latest'
    baseImage = 'openjdk:alpine'
    push = false // Would push to a Docker Hub account
    applicationName = jar.baseName
    addFile {
        from "${projectDir}/src/main/resources/workspace-int.gft.com.cer"
        into '/'
    }
    runCommand 'keytool -importcert -alias workspace-int.gft.com-1 -keystore "$JAVA_HOME/jre/lib/security/cacerts" -storepass changeit -file /workspace-int.gft.com.cer -noprompt'
    addFile {
        from jar
        rename {'app.jar'}
    }
    entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar'])
}

// Avoids distribution version to be changed from ALL to BIN in gradle-wrapper.properties
wrapper.gradleVersion = gVersion
wrapper.distributionType = Wrapper.DistributionType.ALL